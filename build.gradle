plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}
repositories {
    jcenter()
}

group = "de.turban.gradle"
version = '1.0.0'

ext.javaVersion = '1.8'
ext.junitVersion = '5.4.2'
ext.asmVersion = '7.1'

gradlePlugin {
    plugins {
        serviceProviderPlugin {
            id = 'de.turban.gradle.serviceprovider'
            implementationClass = 'de.turban.gradle.serviceprovider.ServiceProviderPlugin'
        }
    }
}

dependencies {
    implementation "org.ow2.asm:asm:${asmVersion}"

    testImplementation gradleTestKit()
    testImplementation("org.spockframework:spock-core:1.3-groovy-2.4"){
        exclude group:"org.codehaus.groovy"
    }
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
    dependsOn("pluginUnderTestMetadata")
}

sourceCompatibility = rootProject.ext.javaVersion
tasks.withType(JavaCompile).configureEach{
    targetCompatibility = rootProject.ext.javaVersion
    options.encoding = 'UTF-8'
}
tasks.withType(GroovyCompile).configureEach{
    groovyOptions.encoding = 'UTF-8'
    groovyOptions.optimizationOptions.indy = true
}


